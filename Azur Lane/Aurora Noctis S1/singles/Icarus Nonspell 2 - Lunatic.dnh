#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["AL Aurora Noctis S1 - Icarus Nonspell 2 - Lunatic"]
#Text["Azur Lane - Aurora Noctis[r]Stage 1 Boss"]
#BGM["./../music/A Tiny, Tiny, Clever Commander.ogg"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#include "script/Skip Scripts/common/AllStarShot/AllStarShot_Const.dnh"
#include "script/Skip Scripts/common/libraries/skip_general_lib.dnh"

let objBoss;

@Event{
	alternative(GetEventType())
		case(EV_REQUEST_LIFE){ SetScriptResult(2900); }
    case(EV_REQUEST_TIMER){ SetScriptResult(25); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(80000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 120, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgBoss = GetCurrentScriptDirectory() ~ "../images/IcarusChibi.png";
	ObjPrim_SetTexture(objBoss, imgBoss);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 57, 73); 			
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(60);
	shoot;
	wait(120);
	movement;
	
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	SetAutoDeleteObject(true);
	killBossPhase(objBoss);
	wait(120);
	CloseScript(GetOwnScriptID());
}


task movement {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		ObjMove_SetDestAtFrame(objBoss, 
			rand(GetCenterX() + 60, GetCenterX() - 60), 
			rand(GetCenterY() - 80, GetCenterY() - 140), 30);
		wait(120);
	}
}

task shoot {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let angleP1 = GetAngleToPlayer(objBoss) + 2;
		let angleP2 = GetAngleToPlayer(objBoss) - 2;
		loop(16) {
			ascent(i in 0..2) {
				gun(angleP1 + i*180);
				gun(angleP2 + i*180);
			}
			spam;
			angleP1 += 360/12;
			angleP2 -= 360/12;
			wait(20);
		}
		spam;
		wait(45);
	}
}

task gun(angle) {
	let bossX = ObjMove_GetX(objBoss);
	let bossY = ObjMove_GetY(objBoss);
	let shotCount = 1;
	
	loop(10) {
		let anglePP = angle;
		if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}
		PlaySound("Atk4", 40, 10);
		loop(shotCount) {
			CreateShotA2(bossX, bossY, 10, anglePP - shotCount + 1, -0.25, 4, 127, 10);
			anglePP += 2;
		}
		shotCount += 1;
		wait(5);
	}
}

task spam {
	let bossX = ObjMove_GetX(objBoss);
	let bossY = ObjMove_GetY(objBoss);
	loop(2){
		let shotX = bossX + rand(-30, 30);
		let shotY = bossY + rand(-40, 40);
		let angleR = rand(0, 359);
		if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}
		PlaySound("Atk3", 30, 20);
		loop(15) {
			CreateShotA2(shotX, shotY, 11, angleR, -0.35, 2, 48, 20);
			angleR += 360/15;
		}
		wait(8);
	}
}

