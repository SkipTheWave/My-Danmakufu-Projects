#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Chen Hai Nonspell 2 (Hard)"]
#Text[""]
#BGM["./../music/06-Shanghai Alice of Meiji 17.ogg"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#include "script/Skip Scripts/common/AllStarShot/AllStarShot_Const.dnh"
#include "script/Skip Scripts/common/libraries/skip_general_lib.dnh"

let objBoss;

@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){ SetScriptResult(9000); }
    case(EV_REQUEST_TIMER){ SetScriptResult(50); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(300000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY() - 80, 70);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgBoss = GetCurrentScriptDirectory() ~ "../art/Chen_HaiSpringChibi.png";
	ObjPrim_SetTexture(objBoss, imgBoss);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 74, 74); 			
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(90);
	main;
	wait(380);
	butterflies;
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	SetAutoDeleteObject(true);
	killBossPhase(objBoss);
	wait(120);
	CloseScript(GetOwnScriptID());
}




task main {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		moveX(rand(GetCenterX() - 90, GetCenterX() + 90));
		wait(60);
		shootA;
		wait(480);
	}
}

task moveX(x) {
	ObjMove_SetDestAtWeight(objBoss, x, GetCenterY() - 80, 15, 2);
}

task shootA {	
	let angleS = rand(0, 360);	
	wing(-1, angleS);
	wing(1, angleS+180);
}

task wing(color, angleS) {
	let graphic = 55;
	if(color == 1) { graphic = 150; }
	let angleDev = 15;
	let obj = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 2, angleS, graphic, 20);
	ObjMove_SetAngularVelocity(obj, 1.6);
	wait(30);
	loop(50) {
		if(Obj_IsDeleted(obj) || ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) { return; }
		ascent(i in 0..3) {
			let objB = CreateShotA2(ObjMove_GetX(obj), ObjMove_GetY(obj), 0.3, ObjMove_GetAngle(obj) - i*angleDev, 0, 0.3, graphic, 0);
			ObjMove_AddPatternA2(objB, 120, 0.1, NO_CHANGE, +0.01 + 0.005*i, 0, 0.7 + 0.2*i);
		}
		wait(5);
	}
	ObjMove_SetAngularVelocity(obj, 0);
}

task butterflies {
	let angle = 0;
	let nshots = 4;
	let nlines = 3;
	let lineAngleDev = 50;
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		wait(10);
		if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) { return; }
		ascent(i in 0..nlines) {
			ascent(j in 0..nshots) {
				CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 1.9 + j*0.1, angle + i*lineAngleDev, 295, 0);
				CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 1.9 + j*0.1, -(angle + i*lineAngleDev) + 180, 295, 0);
			}
		}
		angle += 10;
	}
}



