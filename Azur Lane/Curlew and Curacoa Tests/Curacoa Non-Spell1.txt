#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Curacoa Tutorial Help 1"]
#Text["Curacoa is helping Skip follow Danmakufu tutorials[r]Please be patient with them"]
#BGM["./Aci-L.mp3"]
#include "script/default_system/Default_ShotConst.txt"

let objBoss;

@Event{
    alternative(GetEventType())
    case(EV_REQUEST_LIFE){ SetScriptResult(2000); }
    case(EV_REQUEST_TIMER){ SetScriptResult(30); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(20000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 150, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgCuracoa = GetCurrentScriptDirectory() ~ "CuracoaKaiChibi.png";
	ObjPrim_SetTexture(objBoss, imgCuracoa);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 55, 74); 			// 56x75 sprite
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(60);
	fireA;
	fireB;
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	Obj_Delete(objBoss);
	DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
	SetAutoDeleteObject(true);
	CloseScript(GetOwnScriptID());
}

function GetCenterX(){ return GetStgFrameWidth() / 2; }
function GetCenterY(){ return GetStgFrameHeight() / 2; }
function wait(n){ loop(n){yield;} }

task fireA {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		spiralA(17);
		wait(180);
		spiralA(-17);
		wait(180);
	}
}

task spiralA(angleShift) {
	let angleT = rand(0,360);
	loop(60) {
		CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss) - 90, 2.5, angleT, DS_BALL_M_A_PURPLE, 10);
		CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss) - 90, 2.5, angleT + 180, DS_BALL_M_A_PURPLE, 10);
		angleT += angleShift;
		wait(2);
	}
}

task fireB {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		loop(4) {
			let angleT = GetAngleToPlayer(objBoss);
			ascent(i in -1..2) {
				CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 4.5, angleT + i*4, DS_MISSILE_YELLOW, 20);
			}
			wait(6);
		}
		wait(15);
	}
}
