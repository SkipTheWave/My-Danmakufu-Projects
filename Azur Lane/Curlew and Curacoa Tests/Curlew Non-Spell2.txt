#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Curlew Tutorial Help 3"]
#Text["Curlew is helping Skip follow Danmakufu tutorials[r]Please be patient with them"]
#BGM["./Aci-L.mp3"]
#include "script/default_system/Default_ShotConst.txt"

let objBoss;

@Event{
    alternative(GetEventType())
    case(EV_REQUEST_LIFE){ SetScriptResult(2000); }
    case(EV_REQUEST_TIMER){ SetScriptResult(30); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(20000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgCurlew = GetCurrentScriptDirectory() ~ "CurlewKaiChibi.png";
	ObjPrim_SetTexture(objBoss, imgCurlew);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 55, 74); 			// 56x75 sprite
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(90);
	fireA;
	fireB;
}

task fireA {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let angleA = GetAngleToPlayer(objBoss) - 60;
		ascent(j in 0..2) {
			ascent(i in -5..6) {
				let wave = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 5, angleA + i*6 + j*120, -0.1, 0, DS_SCALE_SKY, 10);
				shotCommandA(wave);
			}
		}
		wait(60);
	}
}

task shotCommandA(obj) {
	wait(60);
	ObjMove_AddPatternA1(obj, 0, 1.5, GetAngleToPlayer(obj));
}

task fireB {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let angleB = GetAngleToPlayer(objBoss) + rand(-15, 15);
		ascent(i in 0..5) {
			let vector = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 2 + i, angleB, -0.04, 0, DS_SCALE_RED, 10);
			shotCommandB(vector);
		}
		wait(180);
	}	
}

task shotCommandB(obj) {
	wait(90);
	ascent(i in 0..16) {
		CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 1, GetAngleToPlayer(obj) + i*360/16, DS_BALL_SS_RED, 20);
	}
	Obj_Delete(obj);
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	Obj_Delete(objBoss);
	DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
	SetAutoDeleteObject(true);
	CloseScript(GetOwnScriptID());
}

function GetCenterX(){ return GetStgFrameWidth() / 2; }
function GetCenterY(){ return GetStgFrameHeight() / 2; }
function wait(n){ loop(n){yield;} }
