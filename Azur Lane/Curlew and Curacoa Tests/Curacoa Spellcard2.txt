#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Curacoa Tutorial Help 4 - \"Ceres-class Suppression B\""]
#Text["Curacoa is helping Skip follow[r]Danmakufu tutorials[r]Please be patient with them"]
#BGM["./Aci-L.mp3"]
#include "script/default_system/Default_ShotConst.txt"
#include "script/Cutin/Cutin.txt"

let objBoss;
let objScene = GetEnemyBossSceneObjectID();

@Event{
    alternative(GetEventType())
    case(EV_REQUEST_LIFE){ SetScriptResult(3000); }
    case(EV_REQUEST_TIMER){ SetScriptResult(60); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(100000); }
}

@Initialize{
    objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	StartSpell;
	
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 75, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgCuracoa = GetCurrentScriptDirectory() ~ "CuracoaKaiChibi.png";
	ObjPrim_SetTexture(objBoss, imgCuracoa);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 55, 74); 			// 56x75 sprite
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(60);
	fireA(3);
	fireA(-3);
	fireB;
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	
	// this part only applies to spellcards, it's the spellcard bonus
	if(ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT)
    +ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT) == 0){
		AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
	}
	 
	Obj_Delete(objBoss);
	DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
	SetAutoDeleteObject(true);
	CloseScript(GetOwnScriptID());
	return;
}

task StartSpell {
	let cutin = GetCurrentScriptDirectory~"CuracoaKai.png";
	ObjCutin_SetSpellcardS4("Curacoa - Ceres-class Suppression B", cutin, NAZRIN, 110, 190, 240);
	ObjCutin_LaunchS3(YABUSAME, cutin, "Hard");
	ObjEnemyBossScene_StartSpell(objScene);
}

task fireA(angularVelocity) {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		ascent(i in 0..60) {
			let angleT = 180 + rand(0, 60);
			ascent(j in 0..4) {
				CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 5, angleT + j*40, DS_RICE_S_ORANGE, 5);
				let curveShots = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 2, angleT + j*40, DS_RICE_S_ORANGE, 5);
				setShotCurve(curveShots, 15, angularVelocity, 45);
			}
			wait(5);
		}
		wait(60);
	}
}
	
task setShotCurve(obj, startDelay, angularVelocity, stopDelay) {
	wait(startDelay);
	ObjMove_SetAngularVelocity(obj, angularVelocity);
	wait(stopDelay);
	ObjMove_SetAngularVelocity(obj, 0);
}

task fireB {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		let angleT = GetAngleToPlayer(objBoss);
		ascent(i in 0..100) {
			let circle = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 2.5, angleT + i*3.6, -0.05, -1, DS_BALL_SS_PURPLE, 15);
		}
		wait(180);
	}
}


function GetCenterX(){ return GetStgFrameWidth() / 2; }
function GetCenterY(){ return GetStgFrameHeight() / 2; }
function wait(n){ loop(n){yield;} }
