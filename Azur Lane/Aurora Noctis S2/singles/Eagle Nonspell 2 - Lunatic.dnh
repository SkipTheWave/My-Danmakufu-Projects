#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["AL Aurora Noctis S2 - Eagle Nonspell 2 - Lunatic"]
#Text["Azur Lane - Aurora Noctis[r]Stage 2 Boss"]
#BGM["./music/Revolt.ogg"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#include "script/Skip Scripts/common/AllStarShot/AllStarShot_Const.dnh"
#include "script/Skip Scripts/common/libraries/skip_general_lib.dnh"

let objBoss;

@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){ SetScriptResult(3200); }
    case(EV_REQUEST_TIMER){ SetScriptResult(30); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(160000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 120, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgBoss = GetCurrentScriptDirectory() ~ "../images/EagleChibi.png";
	ObjPrim_SetTexture(objBoss, imgBoss);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 59, 72); 			
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(60);
	shoot;
	wait(60);
	movement;
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	SetAutoDeleteObject(true);
	killBossPhase(objBoss);
	wait(120);
	CloseScript(GetOwnScriptID());
}


task movement {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		ObjMove_SetDestAtFrame(objBoss, 
			rand(GetCenterX() + 100, GetCenterX() - 100), 
			rand(GetCenterY() - 80, GetCenterY() - 160), 30);
		wait(150);
	}
}

task shoot {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		laser(240, -1);
		wait(90);
		
		laser(300, 1);
		wait(90);
		
		laser(120, -1);
		laser(60, 1);
		wait(130);
		
		laser(240, -1);
		laser(300, 1);
		wait(130);
	}
}

task laser(angle, dir) {
	if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}
	PlaySound("Laser2", 50, 0);
	let obj = CreateCurveLaserA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss),
		5, angle, 40, 12, 1023, 0);
	ObjMove_AddPatternA2(obj, 0, NO_CHANGE, NO_CHANGE, -0.05, 3*dir, 0);
	wait(60);
	
	let angleT = 90;
	loop(7) {
		if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}
		loop(5) {
			if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}
			PlaySound("Atk3", 30, 10);
			ascent(i in 0..4) {
				ascent(j in 1..4) {
					CreateShotA2(ObjMove_GetX(obj), ObjMove_GetY(obj), 1.5, angleT + i*90,
						+0.04*j, 2.8 + j*1.2, 2, 10);
				}
			}
			angleT += 0.8*dir;
			wait(3);
		}
		angleT += 11*dir;
		wait(7);
	}
	Obj_Delete(obj);
}
