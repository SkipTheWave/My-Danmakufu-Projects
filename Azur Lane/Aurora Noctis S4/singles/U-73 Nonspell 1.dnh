#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["AL Aurora Noctis S4 - U-73 Nonspell"]
#Text["Azur Lane - Aurora Noctis[r]Stage 4 Miniboss"]
#BGM["./../music/Ex Nihilo.ogg"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#include "script/Skip Scripts/common/AllStarShot/AllStarShot_Const.dnh"
#include "script/Skip Scripts/common/libraries/skip_general_lib.dnh"

let objBoss;

@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){ SetScriptResult(3300); }
    case(EV_REQUEST_TIMER){ SetScriptResult(23); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(280000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY() - 140, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgBoss = GetCurrentScriptDirectory() ~ "../images/U-73Chibi.png";
	ObjPrim_SetTexture(objBoss, imgBoss);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 75, 75); 			
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(90);
	main;
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	SetAutoDeleteObject(true);
	killBoss(objBoss);
	wait(180);
	CloseScript(GetOwnScriptID());
}




task main {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		loop(3) {
			ObjMove_SetDestAtFrame(objBoss, ObjMove_GetX(GetPlayerObjectID()),
				ObjMove_GetY(GetPlayerObjectID()), 90);
			wait(120);
			let bossX = ObjMove_GetX(objBoss);
			let bossY = ObjMove_GetY(objBoss);
			let nred = 40;
			let angleP = GetAngleToPlayer(objBoss);
			
			if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) { return; }
			PlaySound("Atk4", 50, 20);
			ascent(i in 0..nred) {
				let obj = CreateShotA2(bossX, bossY, 2.5, angleP + i*360/nred, 
					-0.035, 0, 114, 20);
				ObjMove_AddPatternA2(obj, 120, 0, NO_CHANGE, +0.01, 0, 0.5);
			}
			ascent(i in 0..3) {
				ascent(j in 0..7) {
					let obj = CreateShotA2(bossX, bossY, 1 + j/2, angleP + i*360/3, 
						-0.02 - j*0.02/4, 0, 114, 20);
					boom(obj);
				}
			}
			wait(30);
			
		}
		wait(60);
		ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY() - 140, 90);
		wait(90);
		
		ascent(i in 0..99) {
			if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) { return; }
			PlaySound("Twinkle1", 50, 5);
			ascent(j in 0..3) {
				let objB = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 5,
					i*8 + 140 + j, -0.1, 0, 256, 5);
				let objR = CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 5,
					-i*6 + 140 + j, -0.1, 0, 253, 5);
				shotMov(objB, 0.5+j/2);
				shotMov(objR, 1.9+j/2);
			}
			wait(3);
		}
		wait(60);
	}
}

task boom(obj) {		
	wait(60);	
	PlaySound("Atk3", 50, 20);
	if(Obj_IsDeleted(obj) || ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) { return; }
	loop(3) {
		let graphic = [832, 833, 834, 837, 838][rand_int(0, 4)];
		CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), rand(1, 3.2), rand(0, 359), graphic, 20);
	}
	Obj_Delete(obj);
}

task shotMov(obj, maxSpd) {
	ObjMove_AddPatternA2(obj, 45, 0, NO_CHANGE, +0.07, 0, maxSpd);
}



