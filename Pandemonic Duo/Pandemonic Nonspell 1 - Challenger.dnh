#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Pandemonic Duo - Nonspell 1 - Challenger"]
#Text["\"Challenger\" difficulty"]
#BGM["./Music/dBu - Pandemonic Planet.ogg"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#include "./CrispBrightShot/Shotconstants.dnh"
#include "script/Skip Scripts/common/libraries/skip_general_lib.dnh"

let Hecatia;
let Clownpiece;
let startY = GetCenterY() - 100;
let startX_Hecatia = GetCenterX() - 100;
let startX_Clownpiece = GetCenterX() + 100;
let objScene = GetEnemyBossSceneObjectID();

@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){ SetScriptResult(6000); }
    case(EV_REQUEST_TIMER){ SetScriptResult(50); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(300000); }
}

@Initialize{
	SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./system/Pause.dnh");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./system/EndScene.dnh");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./system/ReplaySaveScene.dnh");
	
	Hecatia = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	Clownpiece = ObjEnemy_Create(OBJ_ENEMY);
	ObjEnemy_Regist(Hecatia);
	ObjEnemy_Regist(Clownpiece);
	
	ObjMove_SetDestAtFrame(Hecatia, startX_Hecatia, startY, 60);
	ObjMove_SetDestAtFrame(Clownpiece, startX_Clownpiece, startY, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(Hecatia, ObjMove_GetX(Hecatia), ObjMove_GetY(Hecatia), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(Hecatia, ObjMove_GetX(Hecatia), ObjMove_GetY(Hecatia), 24);
	
	ObjEnemy_SetIntersectionCircleToShot(Hecatia, ObjMove_GetX(Clownpiece), ObjMove_GetY(Clownpiece), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(Hecatia, ObjMove_GetX(Clownpiece), ObjMove_GetY(Clownpiece), 24);
	yield;
}

task TDrawLoop {
	let imgHecatia = GetCurrentScriptDirectory() ~ "Images/Hecatia2-s.png";
	let imgClownpiece = GetCurrentScriptDirectory() ~ "Images/Clownpiece4-s.png";
	ObjPrim_SetTexture(Hecatia, imgHecatia);
	ObjPrim_SetTexture(Clownpiece, imgClownpiece);
	ObjSprite2D_SetSourceRect(Hecatia, 1, 1, 52, 94); 	
	ObjSprite2D_SetSourceRect(Clownpiece, 1, 1, 64, 86); 		
	ObjSprite2D_SetDestCenter(Hecatia);
	ObjSprite2D_SetDestCenter(Clownpiece);
}

task MainTask {
	wait(120);
	main;
}

task TFinalize {
	while(ObjEnemy_GetInfo(Hecatia, INFO_LIFE) > 0) { yield; }
	Obj_Delete(Hecatia);
	Obj_Delete(Clownpiece);
	DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
	SetAutoDeleteObject(true);
	
	PlaySound("Defeat", 50, 0);
	wait(120);
	CloseScript(GetOwnScriptID());
}


task main {
	while(!Obj_IsDeleted(Hecatia)) {
		shootChains;
		wait(60);
		
		let angleT = rand(0, 359);
		shootStars(STAR_M_PINK, -1, angleT);
		wait(15);
		shootStars(STAR_M_RED, 1, angleT);
		wait(300);
	}
}

task moveHecatia() {
	let newX = rand(startX_Hecatia - 80, startX_Hecatia + 160);
	let newY = rand(startY - 40, startY + 40);
	ObjMove_SetDestAtWeight(Hecatia, newX, newY, 45, 5);
}

task moveClownpiece() {
	let newX = rand(startX_Clownpiece - 160, startX_Clownpiece + 80);
	let newY = rand(startY - 40, startY + 40);
	ObjMove_SetDestAtWeight(Clownpiece, newX, newY, 45, 5);
}

task shootChains {
	let angleT = GetAngleToPlayer(Hecatia);
	let chainNum = 8;
	let chainLength = 60;
	
	loop(chainLength) {
		if(Obj_IsDeleted(Hecatia)) { return; }
		PlaySound("Twinkle1", 40, 10);
		ascent(i in 0..chainNum) {
			let obj = CreateShotA1(ObjMove_GetX(Hecatia), ObjMove_GetY(Hecatia), 
				5.5, angleT + i*360/chainNum, KUNAI_M_PURPLE_INV, 20);
		}
		wait(5);
		angleT += -0.8;
	} 
}

task shootStars(graphic, dir, angleT) {
	let starNum = 10;
	let waveNum = 7;

	loop(waveNum) {
		if(Obj_IsDeleted(Clownpiece)) { return; }
		PlaySound("Atk3", 35, 10);
		ascent(i in 0..starNum) {
			let obj = CreateShotA1(ObjMove_GetX(Clownpiece), ObjMove_GetY(Clownpiece), 
				2.5, angleT + i*360/starNum, graphic, 10);
			ObjMove_AddPatternA2(obj, 30, NO_CHANGE, NO_CHANGE, 0, 3*dir, 2);
			ObjMove_AddPatternA2(obj, 90, NO_CHANGE, NO_CHANGE, 0, 0.5*dir, 2);
		}
		wait(30);
		angleT -= 6*dir;
	}
}



