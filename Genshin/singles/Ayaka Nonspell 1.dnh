#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Genshin - Ayaka - Nonspell 1"]
#Text[""]
#BGM["./../music/Inazuma Battle 2.ogg"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#include "script/Skip Scripts/common/AllStarShot/AllStarShot_Const.dnh"
#include "script/Skip Scripts/common/libraries/skip_general_lib.dnh"

let objBoss;

@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){ SetScriptResult(4000); }
    case(EV_REQUEST_TIMER){ SetScriptResult(25); }
    case(EV_REQUEST_SPELL_SCORE){ SetScriptResult(200000); }
}

@Initialize{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetCommonData("bossActive", 1);
	
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 120, 60);
    TDrawLoop; 
    TFinalize;
    MainTask;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	yield;
}

task TDrawLoop {
	let imgBoss = GetCurrentScriptDirectory() ~ "../images/ayaka-sprite.png";
	ObjPrim_SetTexture(objBoss, imgBoss);
	ObjSprite2D_SetSourceRect(objBoss, 1, 1, 55, 113); 			
	ObjSprite2D_SetDestCenter(objBoss);
}

task MainTask {
	wait(60);
	blue;
	white;
	//movement;
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) { yield; }
	SetAutoDeleteObject(true);
	
	killBossPhase(objBoss);
	wait(120);
	CloseScript(GetOwnScriptID());
}


task movement {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		ObjMove_SetDestAtFrame(objBoss, 
			rand(GetCenterX() + 50, GetCenterX() - 50), 
			rand(GetCenterY() - 80, GetCenterY() - 100), 30);
		wait(240);
	}
}

task blue {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		PlaySound("Atk3", 30, 0);
		let angle = 0;
		let spawnAngle = -90;
		let radius = 50;
		ascent(j in 0..8) {
			ascent(i in 0..180) {	
				let x = ObjMove_GetX(objBoss) + radius*cos(spawnAngle);
				let y = ObjMove_GetY(objBoss) + radius*sin(spawnAngle);
				CreateShotA2(x, y, 0, angle, +0.04, 4, 24, 30);
				spawnAngle -= 20;
				wait(2);
				angle += 1 + 4*sin(i);
			}
		}
		wait(120);
	}
}

task white {
	let angle = 0;
	let radius = 100;		// pixels
	let ncyclones = 2;
	ascent(j in 0..ncyclones) {
		let angleCyclone = angle + j*360/ncyclones;
		let x = ObjMove_GetX(objBoss) + radius*cos(angleCyclone);
		let y = ObjMove_GetY(objBoss) + radius*sin(angleCyclone);
		let wvel = 0.5;		// degrees per frame
		let spd = wvel*radius*(pi()/180);		// pixels per frame
		let obj = CreateShotA1(x, y, spd, angleCyclone + 90, 224, 20);
		flakeTask(obj, wvel, j);
	}
}

task flakeTask(obj, wvel, n) {
	ObjMove_SetAngularVelocity(obj, wvel);
	let nwaves = 4;
	let angle = ObjMove_GetAngle(obj);
	while(!Obj_IsDeleted(obj) && ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
		loop(60) {
			PlaySound("Twinkle1", 20, 15);
			ascent(i in 0..nwaves) {
				CreateShotA2(ObjMove_GetX(obj), ObjMove_GetY(obj), 5, angle + i*360/nwaves, -0.1, 1.5 + n, 224, 15);
			}
			angle -= 11;
			wait(3);
		}
		wait(120);
	}
}


